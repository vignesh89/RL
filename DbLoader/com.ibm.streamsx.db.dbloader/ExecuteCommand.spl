
/* Copyright (C) 2013-2015, International Business Machines Corporation  */
/* All Rights Reserved                                	                 */
namespace com.ibm.streamsx.db.dbloader ;

use com.ibm.streamsx.linuxshell::* ;

/**
 * The composite **ExecuteCommand** executes a shell command via LinuxSource.
 * 
 * The LinuxSource operator is a part of the com.ibm.streamsx.linuxshell toolkit.
 * 
 * It measures the execution time and writes the command and its result in a log file. 
 *
 * @input Command rstring command
 * @output Result rstring result
 * @output ExecuteCommandLog rstring result (log information)
 */
composite ExecuteCommand(output Result, ExecuteCommandLog ; input Command)
{
	graph

	/**
		 * StartTime
		 * Get the current time as Start Time to calculate the execution time.
		 */
		stream<rstring command, int64 counter, timestamp startTime> StartTime = Custom(Command)
		{
			logic
				onTuple Command :
				{
					submit({ command = command, counter = counter, startTime = getTimestamp() }, StartTime) ;
				}

		}

		/**
		 * GetCommand
		 * Use Custom operator to deliver the command.
		 */
		stream<rstring command> GetCommand = Custom(StartTime)
		{
			logic
				onTuple StartTime :
				{
					submit({ command = command }, GetCommand) ;
				}

		}

		/**
		 * ExecuteCmd
		 * Use LinuxSource operator to execute a command.
		 * The LinuxSource operator is a part of the com.ibm.streamsx.linuxshell toolkit.
		 */
		(stream<rstring resultLine> ExecuteCmd ; stream<rstring errorLine> StdErr) =
			LinuxSource(GetCommand)
		{
			logic
				onTuple GetCommand :
				{

				}

		}

		/**
		 * EndTime
		 * Get the current time as End Time to calculate the execution time.
		 */
		stream<rstring result, timestamp endTime> EndTime = Custom(ExecuteCmd ; StdErr)
		{
			logic
				state :
				{
					mutable rstring resultLines = "" ;
					mutable rstring stdOut = "" ;
				}

				onTuple ExecuteCmd :
				{
				//	printStringLn("ExecuteCmd resultLine : " + resultLine) ;
					if(length(resultLine) > 0)
					{
						resultLines = resultLines + resultLine + "\n" ;
					}

				}

				onTuple StdErr :
				{
				//	printStringLn("ExecuteCmd errorLine : " + errorLine) ;
					if(length(errorLine) > 1)
					{
						resultLines += errorLine + "\n" ;
					}

				}

				onPunct ExecuteCmd :
				{
					// printStringLn("ExecuteCmd onPunct : " + resultLines) ;
					submit({ result = resultLines, endTime = getTimestamp() }, EndTime) ;
					resultLines = "" ;
				} // End of onPunct ExecuteCmd

		}

		/**
		 * GetStartEndTime
		 * The Barrier operator is used to synchronize tuples from two streams 
		 * StartTime and EndTime.
		*/
		stream<StartTime, EndTime> GetStartEndTime = Barrier(StartTime ; EndTime)
		{
		}

		/**
		 * CreateLog
		 * Creates log information from command and result.
		*/
		stream<rstring result> ExecuteCommandLog = Functor(GetStartEndTime)
		{
			logic
				state :
				{
					mutable int32 findPassword ;
					mutable int32 index1;
					mutable int32 index2;
					mutable rstring cmdAndResult = "" ;
					mutable rstring cmd = "" ;
					mutable rstring passWord = "";
					mutable rstring executeResult ;
					mutable int64 Counter ;
					mutable float64 executionTime = 0.0 ;
				}

				onTuple GetStartEndTime :
				{
					executionTime = diffAsSecs(endTime, startTime) ;
					if(length(result) < 1)
					{
						executeResult = "done\n" ;
					}

					else
					{
						executeResult = result ;
					}

					cmd = command ;
					// hide db2 password in log file
					findPassword = findFirst(command, " using ", 0) ;
					if(findPassword != - 1)
					{
						index1 = findFirst(command, "\"", findPassword) ;
						if(index1 != - 1)
						{
							index2 = findFirst(command, "\"", index1 +1) ;
							if(index2 != - 1)
							{
								cmd = substring(command, 0, index1 +1) + "*******";
								cmd = cmd + substring(command, index2, length(command)-1);
							}
						}
					}

					Counter = counter ;
					//					Counter =(int32)(random() * 100.0) ;
					cmdAndResult = "[" +(rstring) Counter + "] Command: " + cmd + "\n                    ["
						+(rstring) Counter + "] Result : " + executeResult + "Execution Time " +(rstring)
						executionTime + " seconds\n" ;
				}

			output
				ExecuteCommandLog : result = cmdAndResult ;
		}

		/**
		 * Result
		 * Send the execution command result as Output from Composite ExecuteCommand. 
		 */
		stream<rstring result> Result = Functor(GetStartEndTime)
		{
			logic
				state :
				{
					mutable rstring endResult = "" ;
				}

				onTuple GetStartEndTime :
				{
					endResult = result ;
				}

			output
				Result : result = endResult ;
		}

} // End of Composite ExecuteCommand 
